plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
}

repositories {
    jcenter()
}

dependencies {

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.+'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.2'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.2'
}


jacoco{
    toolVersion='0.8.2'
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = false
        csv.enabled = false
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(
                    dir: it,
                    exclude: [
                            '**/domain/*.class',
                            '**/utils/*.class'
                    ]
            )
        })
    }
}

jacocoTestCoverageVerification {
    violationRules {

        rule {
            element = 'CLASS'
            excludes = [
                    '**.domain.*',
                    '**.utils.*'
            ]

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 1
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1
            }
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1
            }
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1
            }
        }
    }
    dependsOn(jacocoTestReport)
}

check.dependsOn jacocoTestCoverageVerification